import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'org.jetbrains.kotlin.jvm'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    maven { url("/Users/jsproch/kotlin-teams/mavenout") }
    maven { url("https://maven.pkg.jetbrains.space/kotlin/p/kotlin/bootstrap/") }
    mavenCentral()
}

dependencies {
    compileOnly(project(":unpackaged"))
}

TaskProvider<ShadowJar> shadowJar = tasks.register("embeddedPlugin", ShadowJar) {
    configurations = [project.configurations.compileClasspath]
    relocate("com.intellij", "org.jetbrains.kotlin.com.intellij")
    archiveBaseName.set("embedded")
    archiveVersion.set("")
    destinationDirectory.set(new File(buildDir, "repackaged"))
}

configurations {
    // replace the standard jar with the one built by 'shadowJar' in both api and runtime variants
    apiElements.outgoing.artifacts.clear()
    apiElements.outgoing.artifact(shadowJar.flatMap {it.archiveFile})
    runtimeElements.outgoing.artifacts.clear()
    runtimeElements.outgoing.artifact(shadowJar.flatMap {it.archiveFile})
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
